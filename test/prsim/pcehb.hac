/**
	\file "pcehb.hac"
	Includes a few buffers with precharges.
**/
import "../../lib/channel.hac";

/**
	half-buffer
 */
defproc pcehb_buf(bool? _Reset; e1of2? L; e1of2! R){
_c1of2 _r;
bool rv, _lv, lv, _en, en;
prs {
	en				-> @_in-
	~@_in &{+~en} L.d[0]		-> _r.d[0]-
	~@_in & L.d[1]			-> _r.d[1]-
	
(:i:2:
	~en				-> _r.d[i]+
	_r.d[i]				=> R.d[i]-
)
	_r.d[0] & _r.d[1]		=> rv-
	L.d[0] | L.d[1]			=> _lv-
	_lv				=> lv-

	rv & lv				#> L.e-
	_Reset & R.e & L.e		-> _en-
	~_Reset				-> _en+
	~R.e & ~L.e			-> _en+
	_en				=> en-
}
}

//- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
/**
	full-buffer
 */
defproc pcefb_buf(bool? _Reset; e1of2? L; e1of2! R){
_c1of2 _r;
bool _en, en, rv, lv, _lv;
prs {
	en				-> @_in-
	~@_in &{+~en} L.d[0]		-> _r.d[0]-
	~@_in & L.d[1]			-> _r.d[1]-
(:i:2:
	~en				-> _r.d[i]+
	_r.d[i]				=> R.d[i]-
)

	_r.d[0] & _r.d[1]		=> rv-
	L.d[0] | L.d[1]			=> _lv-
	_lv				=> lv-
	en & rv & lv			-> L.e-
	~_Reset				-> L.e+
	~en & ~lv			-> L.e+
	_Reset & L.e & R.e		-> _en-
	~L.e & ~R.e			-> _en+
	~_Reset				-> _en+
	_en				-> en-
	~_en & ~rv			-> en+
}
}

