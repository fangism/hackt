import "channel.hac";

// WCHB no Reset
defproc wchb_buf(e1of2 r, l){
e1of2 _r,_l;
prs {
  r.e & l.d[0] -> _r.d[0]-
  ~r.e & ~l.d[0] -> _r.d[0]+
  r.e & l.d[1] -> _r.d[1]-
  ~r.e & ~l.d[1] -> _r.d[1]+
  _r.d[0] -> r.d[0]-
  ~_r.d[0] -> r.d[0]+
  _r.d[1] -> r.d[1]-
  ~_r.d[1] -> r.d[1]+
  _r.d[0] & _r.d[1] -> _l.e-
  ~_r.d[0] | ~_r.d[1] -> _l.e+
  _l.e -> l.e-
  ~_l.e -> l.e+
}
}

// WCHB with Reset high
defproc wchb_buf_rh(e1of2 r, l; bool _Reset){
e1of2 _r,_l;
prs {
  _Reset & r.e & l.d[0] -> _r.d[0]-
  ~_Reset | (~r.e & ~l.d[0]) -> _r.d[0]+
  _Reset & r.e & l.d[1] -> _r.d[1]-
  ~_Reset | (~r.e & ~l.d[1]) -> _r.d[1]+
  _r.d[0] -> r.d[0]-
  ~_r.d[0] -> r.d[0]+
  _r.d[1] -> r.d[1]-
  ~_r.d[1] -> r.d[1]+
  _r.d[0] & _r.d[1] -> _l.e-
  ~_r.d[0] | ~_r.d[1] -> _l.e+
  _l.e -> l.e-
  ~_l.e -> l.e+
}
}

// PCHB no reset
defproc pchb_buf(a1of2 r, l){
a1of2 _r, _l;
prs {
  r.d[0] -> _r.d[0]-
  ~r.d[0] -> _r.d[0]+
  r.d[1] -> _r.d[1]-
  ~r.d[1] -> _r.d[1]+
  r.a & l.a -> r.d[0]-
  ~r.a & ~_l.d[0] -> r.d[0]+
  r.a & l.a -> r.d[1]-  
  ~r.a & ~_l.d[1] -> r.d[1]+
  _l.d[0] & _l.d[1] & _r.d[0] & _r.d[1] -> l.a-
  ~_r.d[0] | ~_r.d[1] -> l.a+
  l.d[0] -> _l.d[0]-
  ~l.d[0] -> _l.d[0]+
  l.d[1] -> _l.d[1]-
  ~l.d[1] -> _l.d[1]+
}
}

// PCHB with Reset high
defproc pchb_buf_rh(a1of2 r, l; bool Reset){
a1of2 _r, _l;
prs {
  r.d[0] -> _r.d[0]-
  ~r.d[0] -> _r.d[0]+
  r.d[1] -> _r.d[1]-
  ~r.d[1] -> _r.d[1]+
  Reset | (r.a & l.a) -> r.d[0]-
  ~Reset & ~r.a & ~_l.d[0] -> r.d[0]+
  Reset | (r.a & l.a) -> r.d[1]-
  ~Reset & ~r.a & ~_l.d[1] -> r.d[1]+
  Reset | (_l.d[0] & _l.d[1] & _r.d[0] & _r.d[1]) -> l.a-
  ~Reset & (~_r.d[0] | ~_r.d[1]) -> l.a+
  l.d[0] -> _l.d[0]-
  ~l.d[0] -> _l.d[0]+
  l.d[1] -> _l.d[1]-
  ~l.d[1] -> _l.d[1]+
}
}

// PCFB no Reset
defproc pcfb_buf(e1of2 r, l){
e1of2 _r, _l;
bool en;
prs {
  l.d[0] & r.e -> _r.d[0]-
  ~en & ~r.e -> _r.d[0]+
  _r.d[0] -> r.d[0]-
  ~_r.d[0] -> r.d[0]+
  l.d[1] & r.e -> _r.d[1]-
  ~en & ~r.e -> _r.d[1]+
  _r.d[1] -> r.d[1]-
  ~_r.d[1] -> r.d[1]+
  r.d[0] | r.d[1] -> l.e-
  ~en & ~l.d[0] & ~l.d[1] -> l.e+
  l.e -> _l.e-
  ~l.e -> _l.e+
  _l.e -> en-
  ~r.d[0] & ~ r.d[1] & ~_l.e -> en+
}
}

// PCFB with Reset high
defproc pcfb_buf_rh(e1of2 r, l; bool _Reset){
e1of2 _r, _l;
bool en;
prs {
  _Reset & l.d[0] & r.e -> _r.d[0]-
  ~_Reset | (~en & ~r.e) -> _r.d[0]+
  _r.d[0] -> r.d[0]-
  ~_r.d[0] -> r.d[0]+
  _Reset & l.d[1] & r.e -> _r.d[1]-
  ~_Reset | (~en & ~r.e ) -> _r.d[1]+
  _r.d[1] -> r.d[1]-
  ~_r.d[1] -> r.d[1]+
  _Reset & r.d[0] | r.d[1] -> l.e-
  ~_Reset | (~en & ~l.d[0] & ~l.d[1]) -> l.e+
  l.e -> _l.e-
  ~l.e -> _l.e+
  _l.e -> en-
  ~r.d[0] & ~ r.d[1] & ~_l.e -> en+
}
}
